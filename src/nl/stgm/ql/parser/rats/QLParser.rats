module nl.stgm.ql.parser.rats.QLParser;
import nl.stgm.ql.parser.rats.QLExpr;
import nl.stgm.ql.parser.rats.QLUtil;

header {
import java.util.List;
import java.util.ArrayList;
import xtc.util.Action;
import nl.stgm.ql.ast.*;
import nl.stgm.ql.ast.types.*;
import nl.stgm.ql.ast.expr.*;
import nl.stgm.ql.ast.form.*;
}

public Expr ParseExpr = yyValue:Expr;
public Form ParseForm = yyValue:Form;
public Document Parse = yyValue:Document;

Document Document = Spacing
	forms:Form*
	{ yyValue = new Document(forms.list()); };

Form Form =
	"form" Spacing id:Ident Spacing '{' Spacing items:FormItem* '}' Spacing
	{ yyValue = new Form(id, items.list()); };

FormItem FormItem =
	Question / Conditional;

Question Question =
	CalcQuestion /
	id:Ident ':' Spacing question:StringLit Spacing type:Ident Spacing
	{ yyValue = new Question(id, question, type); };

CalcQuestion CalcQuestion =
	id:Ident ':' Spacing question:StringLit Spacing type:Ident Spacing '(' Spacing calculation:Expr ')' Spacing
	{ yyValue = new CalcQuestion(id, question, type, calculation); };

//
// nesting of conditionals is forbidden, so only Question* in here
// this could be allowed, but a nesting limit should probably be agreed on
//
Conditional Conditional =
	"if" Spacing '(' Spacing condition:Expr ')' Spacing '{' Spacing questions:Question* '}' Spacing
	{ yyValue = new Conditional(condition, questions.list()); };
