module nl.stgm.ql.parser.rats.QLExpr;
import nl.stgm.ql.parser.rats.QLLexical;

ASTExpressionNode ASTExpressionNode =
	l:RelExpr "&&" Spacing r:RelExpr { yyValue = new And(l, r); } /
	l:RelExpr "||" Spacing r:RelExpr { yyValue = new Or(l, r); }  /
	yyValue:RelExpr; 
    
ASTExpressionNode RelExpr =
	l:AddExpr "<=" Spacing r:AddExpr { yyValue = new LEq(l, r); } /
	l:AddExpr ">=" Spacing r:AddExpr { yyValue = new GEq(l, r); } /
	l:AddExpr "<"  Spacing r:AddExpr { yyValue = new LT(l, r); }  /
	l:AddExpr ">"  Spacing r:AddExpr { yyValue = new GT(l, r); }  /
	l:AddExpr "==" Spacing r:AddExpr { yyValue = new Eq(l, r); }  /
	l:AddExpr "!=" Spacing r:AddExpr { yyValue = new NEq(l, r); } /
	yyValue:AddExpr;
 
ASTExpressionNode AddExpr =
	seed:MulExpr actions:AddTail*
	{
		yyValue = apply(actions, seed);
	};

constant Action<ASTExpressionNode> AddTail = 
	"+" Spacing right:MulExpr
	{ 
		yyValue = new Action<ASTExpressionNode>() {
			public ASTExpressionNode run(ASTExpressionNode left) {
				return new Add(left, right);
			}
		};
	}
	/ "-" Spacing right:MulExpr
	{
		yyValue = new Action<ASTExpressionNode>()
		{
			public ASTExpressionNode run(ASTExpressionNode left)
			{
				return new Sub(left, right);
			}
		};
	};

ASTExpressionNode MulExpr =
	seed:Unary actions:MulTail*
	{
		yyValue = apply(actions, seed);
	};

constant Action<ASTExpressionNode> MulTail =
	"*" Spacing right:Unary
	{ 
		yyValue = new Action<ASTExpressionNode>() {
			public ASTExpressionNode run(ASTExpressionNode left) {
				return new Mul(left, right);
			}
		};
	}
	/ "/" Spacing right:Unary
	{ 
		yyValue = new Action<ASTExpressionNode>()
		{
			public ASTExpressionNode run(ASTExpressionNode left)
			{
				return new Div(left, right);
			}
		};
	};

ASTExpressionNode Unary = 
	"+" Spacing un:Unary { yyValue = new Pos(un); } /
	"-" Spacing un:Unary { yyValue = new Neg(un); } /
	"!" Spacing un:Unary { yyValue = new Not(un); } /
	yyValue:Primary;

ASTExpressionNode Primary =
	n:Int Spacing { yyValue = new IntLit(Integer.parseInt(n)); } /
	b:Bool Spacing { yyValue = new BoolLit(b == "true"); } /
	x:Ident Spacing { yyValue = new Ident(x); } /
	LParen yyValue:ASTExpressionNode RParen;
